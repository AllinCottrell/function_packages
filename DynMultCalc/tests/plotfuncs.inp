function void irf_plot (const bundle b,
                        scalar conflev[0:1:0.95],
                        string ofile[null])
    if !b.cumulate
        funcerr "response must be cumulated"
    endif
    m = b.mult
    plt = (b.sdx * m[,1]) ~ seq(0, rows(m) - 1)'
    bandwidth = invcdf(z, 1 - (1-conflev)/2)
    band = b.sdx * (m[,1] ~ bandwidth * m[,2])
    if exists(ofile)
        targ = sprintf("\"%s\"", ofile)
    else
        targ = "display"
    endif
    plot plt
        option with-lines
        options band=band band-style=fill,grey
        printf "set title 'Response to one-std dev permanent shock in %s'", b.xname
        printf "set ylabel '%s'", b.yname
        literal set xlabel "Horizon"
    end plot --output=@targ
end function

function void multi_plot (const bundle b,
                          scalar conflev[0:1:0.95],
                          string ofile[null])
    m = b.mult
    plt = m[,1] ~ seq(0, rows(m) - 1)'
    cnameset(plt, "Multiplier Horizon")
    bandwidth = invcdf(z, 1 - (1-conflev)/2)
    band = m[,1] ~ bandwidth * m[,2]

    if exists(ofile)
        targ = sprintf("\"%s\"", ofile)
    else
        targ = "display"
    endif

    plot plt
        option with-lines
        options band=band band-style=fill,grey
    end plot --output=@targ
end function
