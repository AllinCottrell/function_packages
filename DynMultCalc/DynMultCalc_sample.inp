set verbose off
clear
include DynMultCalc.gfn

example = 5

if example == 1
    # simple printout
    open denmark --quiet
    ols LRM const LRM(-1) LRY(0 to -2) IBO(0 to -1) IDE(-1) -w
    b = multipliers($model, "IBO")
    multi_print(b)
    
elif example == 2
    # plotting function with custom horizon
    open fedstl.bin
    data gdpc1
    data unrate
    ogap = 100 * hpfilt(log(gdpc1))
    ols unrate const time ogap(0 to -2) unrate(-1 to -2) -w

    horizon = 40
    cumulate = 1
    b = multipliers($model, "ogap", horizon)
    multi_plot(b, cumulate)
    
elif example == 3
    # just the long run effects
    open denmark
    ols LRM const LRM(-1 to -2) LRY(0 to -2) IBO(0 to -1) IDE(0 to -2) --quiet

    # compute the long-run multipliers
    bundle lr_fx = long_run_effects($model)

    # print the long-run multipliers
    long_run_print(lr_fx)
    
elif example == 4
    # pure autoregressive on artificial data
    set seed 7112022
    nulldata 128
    setobs 1 1 --special-time-series
    series y = 0
    y = 1.4 * y(-1) - 0.6 * y(-2) + normal()
    ols y const y(-1 to -2)
    b = multipliers($model, "y")
    multi_print(b)
    
elif example == 5
    # constructing a model "by hand"
    
    open denmark --quiet
    list X = const LRM(-1) LRY(0 to -2)
    matrix coeff = {0; 0.8; 1.2; -0.7; 0.1}
    string yname = "LRM"

    bundle model = _(depvar=yname, xlist=X, xnames=varnames(X),
		     coeff=coeff, vcv=I(5)*0.01)
    
    b = multipliers(model, "LRY")
    # exemplify the optional parameters for multi_plot
    multi_plot(b, 0, 0.9, "example5.pdf")
endif
